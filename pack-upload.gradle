apply plugin: 'maven-publish'

// 压缩生成归档文件
task packZip(type: Zip) {
    archiveBaseName = project.name
    archiveAppendix = VERSION
    archiveClassifier = "all"
    archiveExtension = "zip"
    // 打包根路径
    from project.projectDir.absolutePath
    // 输出路径
    // destinationDir = project.projectDir.absoluteFile
    exclude '**.zip'
    exclude '**.iml'
    exclude '**/**.iml'
    exclude 'build/**'
    exclude '.idea/**'
    exclude '.gradle/**'
    exclude 'gradle/**'
    exclude '**/build/**'
    exclude 'repo/**'
}

afterEvaluate {
    publishing {
        publications {
            debug(MavenPublication) {
                from components.debug

                // 为方便多个 lib 项目上传操作
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION + "-debug"

                // 添加其它文件
                artifact packZip
            }
            release(MavenPublication) {
                from components.release

                // 为方便多个 lib 项目上传操作
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION

                // 添加其它文件
                artifact packZip
            }
        }

        // 定义发布到哪里
        repositories {
            maven {
                // 如果定义多个 maven 必须显示指定 name
                name = "project"
                url = uri("$rootDir/repo/")
            }
            maven {
                name = "Mac"
                // 可以有且仅有一个仓库不指定 name 属性，会隐式设置为 Maven
                url = mavens.local
                // 仓库用户名密码
                credentials {
                    username mavens.localName
                    password mavens.localPwd
                }
            }
            maven {
                name = "emao"
                // 可以有且仅有一个仓库不指定 name 属性，会隐式设置为 Maven
                url = mavens.emao
                // 仓库用户名密码
                credentials {
                    username mavens.emaoName
                    password mavens.emaoPwd
                }
            }
        }
    }
}